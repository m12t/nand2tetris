// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
        // the hierarchy of or precedence is as follows:
        // reset > load > increment > read-only

        // reset has highest priority, so do that operation LAST. reverse the order...

        Inc16(in=previous, out=incremented);                     // 1. `inc` is the lowest priority; `previous` ties the previous output and allows the inc to work
        Mux16(a=previous, b=incremented, sel=inc, out=o1);       // 1. if `inc`==1 increment the previous output. Else pass through the previous value unchanged

        Mux16(a=o1, b=in, sel=load, out=o2);                     // 2. the next priority is `load`. load the new value `in` if load==1 else the output from (1)

        Or(a=inc, b=load, out=incOrLoad);                        // 3. used to determine if an action was previously taken by (1) or (2)

        Mux16(a=in, b=o2, sel=incOrLoad, out=o3);                // 4. based on the value from (3) either use the original `in` or the modified `o3` output
        Mux16(a=o3, b=false, sel=reset, out=o4);                 // 5. this takes precedence over other actions, so it is performed "last" (it's combinational logic, but still occurs later in the circuit)

        Or(a=reset, b=incOrLoad, out=anyAction);                 // 6. used to determine if an action was previously taken by (1), (2), or (5)
        Register(in=o4, load=anyAction, out=previous, out=out);  // 7. based on (6) load the new value if `load`, `inc`, or `reset` were high. else read-only.

}
